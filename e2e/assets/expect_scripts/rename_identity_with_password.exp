#!/usr/bin/expect -df
#
# This Expect script was generated by autoexpect on Wed Mar  9 13:51:53 2022
# Expect and autoexpect were both written by Don Libes, NIST.

match_max 100000
set timeout 30

# try creating the identity alice until it is created
while {1} {
	spawn dfx identity new alice
	expect {
		"Please enter a passphrase for your identity: " {
			send -- "testpassword\r"
			expect {
				"Created identity: \"alice\"." {
					expect eof {
						break
					}
				}
			}
		}
		"Error: Identity already exists." {
			puts stderr "Previous identity creation attempt was botched. Clean up broken identity"
			exec rm -rf "$env(DFX_CONFIG_ROOT)/.config/dfx/identity/alice"
		}
		timeout {
			puts stderr "Not asked for a password when creating new identity!"
			exit 1
		}
	}
}

spawn dfx identity rename alice bob
expect {
	-regex "Renamed identity" {}
	timeout {
		puts stderr "Failed to rename encrypted identity!"
		exit 1
	}
}
expect eof

spawn dfx identity use bob
expect eof

spawn dfx identity get-principal
expect -exact "\rPlease enter a passphrase for your identity: "
send -- "testpassword\r"
expect {
	"Decryption complete." {
		expect eof
	}
	"Decryption failed." {
		puts stderr "Failed to decrypt renamed identity."
		exit 1
	}
}
